plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'dev.bitbite'
version = '2.0.2'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId = 'dev.bitbite'
            artifactId = 'OpenNetLib'
			      version = '2.0.2'
            pom {
                name = 'OpenNetLib - A Java Networking Library'
                description = 'OpenNetLib is a lightweight, easy-to-use library to simplify socket communication. It is written in and for Java. It\'s still in an early stage but saves a lot of time already. Start using OpenNetLib today or help improving it by posting issues, questions and feature requests here: https://github.com/bitbitedev/OpenNetLib/issues'
                url = 'https://github.com/bitbitedev/OpenNetLib'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/bitbitedev/OpenNetLib/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'bitbite'
                        name = 'Bitbite Team'
                        email = 'team@bitbite.dev'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/bitbitedev/OpenNetLib.git'
                    url = 'https://github.com/bitbitedev/OpenNetLib'
                }
            }

        }
    }
    repositories {
    	maven {
            name = "OSSRH"
    		credentials {
    			username = System.getenv("MAVEN_USERNAME")
    			password = System.getenv("MAVEN_PASSWORD")
    		}
    		def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
    		def snapshotRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    		url = version.endsWith('SNAPSHOT') ? snapshotRepoUrl : releasesRepoUrl
    	}
    }
}

artifacts {
    archives javadocJar, sourcesJar
}
signing {
    sign publishing.publications.maven
}
javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}
dependencies {
    // Use JUnit Jupiter for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter:5.12.0'
    
    //lombok
    compileOnly 'org.projectlombok:lombok:1.18.36'
	annotationProcessor 'org.projectlombok:lombok:1.18.36'
}

tasks.named('test') {
    // Use JUnit Platform for unit tests.
    useJUnitPlatform()
}
